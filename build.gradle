import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.26.0'
}

group 'com.example.openglfx'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

dependencies {
    def lwjglNatives
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        lwjglNatives = "natives-windows"
    } else {
        lwjglNatives = "natives-linux"
    }

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.1")

    implementation 'com.huskerdev:openglfx:4.0'
    implementation 'com.huskerdev:openglfx-lwjgl:4.0'

    // LWJGL
    implementation platform('org.lwjgl:lwjgl-bom:3.3.2')
    implementation 'org.lwjgl:lwjgl'
    implementation 'org.lwjgl:lwjgl-opengl'

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
}

test {
    useJUnitPlatform()
}

javafx {
    version = '17.0.6'
    modules = ['javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.base']
}

run {
    jvmArgs = [
            "--add-opens=javafx.base/com.sun.javafx=ALL-UNNAMED",
            "--add-opens=javafx.graphics/com.sun.prism=ALL-UNNAMED",
            "--add-opens=javafx.graphics/com.sun.prism.d3d=ALL-UNNAMED",
            "--add-opens=javafx.graphics/com.sun.javafx.scene.layout=ALL-UNNAMED",
            "--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
            "--add-opens=javafx.graphics/com.sun.javafx.sg.prism=ALL-UNNAMED",
            "--add-opens=javafx.graphics/com.sun.javafx.tk=ALL-UNNAMED",
            "--add-opens=javafx.graphics/com.sun.javafx.geom=ALL-UNNAMED",
            "--add-opens=javafx.graphics/javafx.scene.image=ALL-UNNAMED"
    ]
}

jlink {
    addExtraDependencies("javafx")
    launcher {
        name = 'Example'
        jvmArgs = [
                "--add-opens=javafx.base/com.sun.javafx=ALL-UNNAMED",
                "--add-opens=javafx.graphics/com.sun.prism=ALL-UNNAMED",
                "--add-opens=javafx.graphics/com.sun.prism.d3d=ALL-UNNAMED",
                "--add-opens=javafx.graphics/com.sun.javafx.scene.layout=ALL-UNNAMED",
                "--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
                "--add-opens=javafx.graphics/com.sun.javafx.sg.prism=ALL-UNNAMED",
                "--add-opens=javafx.graphics/com.sun.javafx.tk=ALL-UNNAMED",
                "--add-opens=javafx.graphics/com.sun.javafx.geom=ALL-UNNAMED",
                "--add-opens=javafx.graphics/javafx.scene.image=ALL-UNNAMED"
        ]
    }
}

mainClassName = 'com.example.openglfx.openglfxmem.HelloApplication'